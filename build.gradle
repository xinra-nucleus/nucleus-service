buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.4.0'
    }
}

plugins {
  id "net.ltgt.apt" version "0.8" //annotation processing
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'com.novoda.bintray-release'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '0.1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'xinra Nucleus Service Layer',
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/xinra/nucleus" }
}

dependencies {
    compile 'com.google.guava:guava:19.0'
    compile 'org.codehaus.groovy:groovy:2.4.7'
    compile 'org.springframework:spring-context:4.3.5.RELEASE'
    compileOnly 'com.xinra.nucleus:interface-generator:0.1.1'
    apt 'com.xinra.nucleus:interface-generator:0.1.1'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

publish {
    userOrg = 'xinra'
    repoName = 'nucleus'
    groupId = 'com.xinra.nucleus'
    artifactId = 'nucleus-service'
    publishVersion = '0.1.0'
    licences = ['BSD 3-Clause']
    desc = 'Nucleus service layer'
    website = 'https://github.com/xinra-nucleus/nucleus-service'
}

sourceSets {
  // put everything into /src/*/groovy
  main.java.srcDirs = []
  test.java.srcDirs = []
}

compileGroovy {
  groovyOptions.javaAnnotationProcessing = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
